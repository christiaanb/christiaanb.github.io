---
layout: post
title:  "Three ways to describe popcount in CλaSH"
date:   2015-10-20 09:10:00
categories: draft
tags: cλash haskell template_haskell dependent_types
comments: false
analytics: false
---

Describing a population counter in CλaSH.

## Original

PopulationCount.hs:
{% highlight haskell %}
module PopulationCount where

import CLaSH.Prelude

popCount :: (KnownNat (n+1), KnownNat (n + 2))
         => BitVector (n+1)
         -> Index (n+2)
popCount bv = sum (map fromIntegral v)
  where
    v = bv2v bv

topEntity :: BitVector 9 -> Index 10
topEntity = popCount
{% endhighlight %}

<br>

## Template Haskell

PopCountTH:
{% highlight haskell %}
module PopCountTH where

import           CLaSH.Prelude
import qualified Data.List           as List
import           Language.Haskell.TH (litT, numTyLit)

popCountTH' []  _  = [| 0 |]
popCountTH' [i] bv = [| (fromIntegral ($(bv)!i) :: Index 2) |]
popCountTH' is  bv = [| $(popCountTH' isL bv) `plus`
                        $(popCountTH' isR bv) |]
  where
    (isL,isR) = List.splitAt (List.length is `div` 2) is

-- | A population counter for BitVector's of lenght 'i'
popCountTH i =
  [| (\bv -> $(popCountTH' (List.reverse [0..(i-1)]) [|bv|]))
     :: BitVector $(litT (numTyLit i))
     -> Index $(litT (numTyLit (i+1))) |]
{% endhighlight %}

PopCount:
{% highlight haskell %}
module PopCount where

import CLaSH.Prelude
import PopCountTH

topEntity = $(popCountTH 9)
{% endhighlight %}

## Dependent Haskell

The Template Haskell solution uses larger adders as it get closer
to the root of tree, something that couldn't be expressed in our
initial solution. In my post on the mailing list I said I would put
this problem on my “Try to solve this using dependent types” TODO
list, and report back if I’m able to figure out  a solution that
wouldn’t need Template Haskell.

I did solve it, and the result can be found below. Sadly, it is
currently not synthesisable by the CλaSH compiler. Also, it only
works for BitVector's whose size is a power of two.

PopCountDep.hs:
{% highlight haskell %}
{-# LANGUAGE GADTs, RankNTypes, ScopedTypeVariables #-}
{-# LANGUAGE UndecidableInstances #-}
module PopCountDep where

import CLaSH.Prelude
import CLaSH.Sized.Internal.Index
import Data.Proxy (Proxy (..))
import Data.Singletons.Prelude
import qualified Data.List as L

-- | Perfect binary tree
data Tree :: Nat -> * -> * where
  Tip    :: a -> Tree 0 a
  Branch :: Tree n a -> Tree n a -> Tree (n+1) a

-- | Convert from a vector of length `2^n` to a perfect binary tree of
-- depth `n`;
fromVector :: KnownNat n => Vec (2 ^ n) a -> Tree n a
fromVector v = fromVector' (toUNat snat) @christiaanb

fromVector' :: UNat n -> Vec (2^n) a -> Tree n a
fromVector' _         (Cons a Nil) = Tip a
fromVector' (USucc s) vs           = Branch (fromVector' s l)
                                            (fromVector' s r)
  where
    (l,r) = splitAtU (powUNat u2 s) vs
    u2    = USucc (USucc UZero)

-- | SplitAtU function that is normally hidden in "CLaSH.Sized.Vector"
splitAtU :: UNat m -> Vec (m + n) a -> (Vec m a, Vec n a)
splitAtU UZero     ys            = (Nil,ys)
splitAtU (USucc s) (y `Cons` ys) = let (as,bs) = splitAtU s ys
                                   in  (y `Cons` as, bs)

-- | A dependently typed tree-fold on perfect binary trees
tfold :: forall p k a .
         Proxy (p :: TyFun Nat * -> *)
         -- ^ The motive
      -> (a -> (p $ 0))
         -- ^ How to fold `Tip`
      -> (forall l . Proxy l -> (p $ l) -> (p $ l) -> (p $ (l+1)))
         -- ^ How to fold `Branch`
      -> Tree k a
      -> (p $ k)
tfold _ f _ (Tip a)                    = f a
tfold m f g (Branch (l :: Tree z a) r) = g (Proxy :: Proxy z)
                                           (tfold m f g l)
                                           (tfold m f g r)

-- | The motive I need for popCount: \l -> Index ((2^l)+1)
data IIndex (f :: TyFun Nat *) :: *
type instance Apply IIndex l = Index ((2^l)+1)

-- | Count the number of bits given a perfect tree of Bits
popCountT :: KnownNat k => Tree k Bit -> Index ((2^k)+1)
popCountT = tfold (Proxy :: Proxy IIndex) fromIntegral
                  (\_ x y -> plus x y)

-- | Count the number of bits in a BitVector of size 2^k
popCount :: (KnownNat k, KnownNat (2^k))
         => BitVector (2^k)
         -> Index ((2^k)+1)
popCount = popCountT . fromVector . bv2v
{% endhighlight %}
